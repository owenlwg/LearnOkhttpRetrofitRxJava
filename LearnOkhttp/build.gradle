import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

android {
    signingConfigs {
        key01 {
            keyAlias key_Alias
            keyPassword key_Password
            storePassword store_Password
            storeFile file(store_File)
        }
    }

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.owen.learn"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix 'release'
            versionNameSuffix '1.1'
            signingConfig signingConfigs.key01
        }
    }

    productFlavors {
        demo {
            minSdkVersion rootProject.ext.minSdkVersion
            applicationId 'com.owen.learndemo'
            targetSdkVersion rootProject.ext.targetSdkVersion

            buildConfigField "String", "CONFIG_ENDPOINT", "\"https://api.github.com\";"
        }
        free {
            applicationId 'com.owen.learnfree'
            targetSdkVersion rootProject.ext.targetSdkVersion

            buildConfigField "String", "CONFIG_ENDPOINT", "\"https://echo.getpostman.com\";"
        }
    }

    //variantFilter过滤指定flavor和buildType
    variantFilter { variant ->
        def names = variant.flavors*.name

        if (names.contains("demo") && variant.buildType.name == "release") {
            variant.ignore = true
        }
    }

    lintOptions{
        abortOnError false
    }

    //修改生成的apk名称
    android.applicationVariants.all { variant ->
        def file = variant.outputs[0].outputFile
        def result = new SimpleDateFormat("yyyyMMddHHmm").format(new Date())
        variant.outputs[0].outputFile = new File(file.parent, file.name.replace(".apk",
                                             "-v" + defaultConfig.versionName + "-" + result
                                             + ".apk"))
    }

}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.2.1'
    compile 'com.android.support:design:23.2.1'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.squareup.okhttp:okhttp:2.7.5'
    compile 'com.facebook.stetho:stetho:1.3.1'
    compile 'com.facebook.stetho:stetho-okhttp:1.3.1'
}
